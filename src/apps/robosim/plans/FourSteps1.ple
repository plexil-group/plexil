// -*- Mode: Java -*-
// This is a Plexil plan for the RoboSim application.  It has the robot
// move one step in each direction.  The plan is successful if and only
// if the start and end positions of the robot are the same.

// This plan is like the one in FourSteps.ple, but instead uses the
// parameterized Move() command.
// Direction 0 is up, 1 is right, 2 is down, and 3 is left.

// Copyright (c) 2006-2010, Universities Space Research Association (USRA).
//  All rights reserved.
//
// Redistribution and use in source and binary forms, with or without
// modification, are permitted provided that the following conditions are met:
//     * Redistributions of source code must retain the above copyright
//       notice, this list of conditions and the following disclaimer.
//     * Redistributions in binary form must reproduce the above copyright
//       notice, this list of conditions and the following disclaimer in the
//       documentation and/or other materials provided with the distribution.
//     * Neither the name of the Universities Space Research Association nor the
//       names of its contributors may be used to endorse or promote products
//       derived from this software without specific prior written permission.
//
// THIS SOFTWARE IS PROVIDED BY USRA ``AS IS'' AND ANY EXPRESS OR IMPLIED
// WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF
// MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
// DISCLAIMED. IN NO EVENT SHALL USRA BE LIABLE FOR ANY DIRECT, INDIRECT,
// INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING,
// BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS
// OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND
// ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR
// TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE
// USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.

Real[3] Command QueryRobotState(String name);
Integer Command Move(String name, Integer direction);

FourSteps:
{
	String RobotName = "RobotYellow";
	Real StartX, StartY, EndX, EndY;
	Integer Index = 0; 
	PostCondition (StartX == EndX) && (StartY == EndY);

    GetStartPosition:
    {
		Real RobotState[3];
        GetRobotState:
        { 
			EndCondition isKnown(RobotState[0]);
			RobotState = QueryRobotState(RobotName);
        }
		StartX = RobotState[0];
		StartY = RobotState[1];
	}
    MoveLoop:
    {
		Integer Directions[4] = #(3 0 1 2);
		RepeatCondition Index < 4;
        Move:
        {
            Integer result;
            EndCondition isKnown(result);
            PostCondition result == 1;
            result = Move(RobotName, Directions[Index]);
        }
		Index = Index + 1;
    }
    GetEndPosition:
    {
		Real RobotState[3];
        GetRobotState:
        { 
			EndCondition isKnown(RobotState[0]);
			RobotState = QueryRobotState(RobotName);
        }
		EndX = RobotState[0];
		EndY = RobotState[1];
    }
}
