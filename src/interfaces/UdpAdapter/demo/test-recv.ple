// -*- Mode: java -*-
// File: plexil/src/interfaces/UdpAdapter/demo/test-recv.ple

test-recv:
{
    Real run_start_time;        // When this run started
    Integer timeout = 5;        // Time out the run
    Boolean continue = true;    // Repeat the HandleCommand node
    Boolean send_ack = true;    // If true, send an ack for test_udp_msg
    // Allow the plan to succeed whether it is ended by a Quit message or a timeout
    UncheckedSequence:
    RecordStartTime:
    {
        Assignment: run_start_time = Lookup(time);
    }
    WaitForTimeoutOrCommand:
    {
        InvariantCondition: continue == true;
        Concurrence:
        HandleCommand:
        {
            RepeatCondition: continue;
            Sequence:
            ReceiveCommand:
            {   //OnCommand does not allow arrays, so arg7 and arg8 are simply left off
                OnCommand test_udp_msg (String arg1, Boolean arg2, Integer arg3, Real arg4, Integer arg5, Real arg6)
                {
                    Sequence:
                    ReportCommand:
                    {
                        Command print ("\ntest-recv: received arg1==", arg1, ", arg2==", arg2, ", arg3==", arg3, ", arg4==", arg4,
                                       ", arg5==", arg5, ", arg6==", arg6, ", arg7==", ", arg8==", "\n\n");
                    }
                    CheckForAckRequest:
                    {
                        Assignment: send_ack = arg2;
                    }
                }
            }
            SendCommandAck:
            {
                SkipCondition: send_ack != true;
                Sequence:
                ReportSendingAck:
                {
                    Command print ("\ntest-recv: sending ack\n\n");
                }
                SendAck:
                {
                    Command ack_msg ("ack");
                }
            }
        }
        HandleTimeout:
        {
            StartCondition: LookupOnChange(time, 1) > run_start_time + timeout;
            UncheckedSequence:
            ReportTimeout:
            {
                Command print ("\ntest-recv: timed out after ", timeout, " seconds\n\n");
            }
            SignalTimeout:
            {
                Assignment: continue = false;
            }
        }
        HandleQuitMessage:
        {
            OnCommand quit_msg (String junk)
            {
                UncheckedSequence:
                ReportQuitReceived:
                {
                    Command print ("\ntest-recv: received quit command\n\n");
                }
                SignalQuit:
                {
                    Assignment: continue = false;
                }
            }
        }
    }
}

// EOF
