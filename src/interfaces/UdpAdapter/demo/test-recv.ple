// -*- Mode: java -*-
// File: plexil/src/interfaces/UdpAdapter/demo/test-recv.ple

test-recv:
{
    Real run_start_time;
    Integer timeout = 5;

    Sequence:

    RecordStartTime:
    {
        Assignment: run_start_time = Lookup(time);
    }

    WaitForTimeoutOrCommand:
    {
        Concurrence:

        WaitForCommand:  // Wait for an incoming UDP message or time out
        {
            InvariantCondition: LookupOnChange(time, 1) < run_start_time + timeout;
            OnCommand test_udp_msg (String arg1, Boolean arg2, Integer arg3, Real arg4, Integer arg5, Real arg6)
            {
                Command print ("\ntest-recv: arg1==", arg1, ", arg2==", arg2, ", arg3==", arg3, ", arg4==", arg4,
                               ", arg5==", arg5, ", arg6==", arg6, ", arg7==", ", arg8==", "\n\n");
            }
        }

        ReportTimeoutFailure: // Report a timeout if such occurs
        {
            StartCondition: WaitForCommand.state == FINISHED;
            SkipCondition: WaitForCommand.outcome != FAILURE;
            Command print ("\ntest-recv: WaitForCommand timed out after ", timeout, " seconds\n\n");
        }
    }
}

// EOF
