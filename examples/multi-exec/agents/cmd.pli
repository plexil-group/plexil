;; File: $SVNROOT/examples/multi-exec/agents/cmd.pli

(plexil-plan
  (sequence "cmd"
    (variables (string "name" "cmd")
               (integer "period" 2)
               (integer "run-length" 160)
               (real-array "actual" 5)
               (real "start-time")
               (boolean "debug" true)
               (boolean "runtime-error" false))
    ;; Record the start time of the run...
    (assignment (realvar "start-time") (lookup-now "time"))
    ;; Poll for the current state and react accordingly
    (for (integer "i" 0) (>= (intvar "run-length") (intvar "i")) (+ (intvar "i") (intvar "period"))
      (sequence "get-current-state"
        (variables (real "elapsed-time"))
        (assignment (realvar "elapsed-time") (- (lookup-now "time") (realvar "start-time")))
        (command "print" "\n[" (stringvar "name") "] elapsed-time: " (intvar "i") " (" (realvar "elapsed-time") ")\n")
        (if (boolvar "runtime-error")
            (sequence "error-sequence"
              (assignment (array-element "actual" 0) 0) ;for use iin the end-condition
              (action "get-states"
                (end-condition (not (= (array-element "actual" 0) 0)))
                (command-with-return (arrayvar "actual") "get-current-state" (intvar "period"))))
          (synchronous-command "get-states"
            (command-with-return (arrayvar "actual") "get-current-state" (intvar "period"))))
        (command "print" "[" (stringvar "name") "] recv: " (arrayvar "actual") "\n")
        (sequence "decel-1"
          (variables (real "kps-decel" -1.21) (real "delta"))
          (skip-condition (or (< 70036.0 (array-element "actual" 0))
                              (> 42796.0 (array-element "actual" 0))
                              (> 180.0 (array-element "actual" 2)))) ;target speed
          (assignment (realvar "delta") (* (realvar "kps-decel") (realvar "period")))
          (command "set-speed" (realvar "delta")))
        (sequence "decel-2"
          (variables (real "kps-decel" -1.1666) (real "kps-delta") (real "dps" 1.2) (real "dps-delta"))
          (skip-condition (or (<= 42796.0 (array-element "actual" 0))
                              (> 110.0 (array-element "actual" 2))))
          (assignment (realvar "kps-delta") (* (realvar "kps-decel") (realvar "period")))
          (assignment (realvar "dps-delta") (* (realvar "dps") (realvar "period")))
          (command "set-speed" (realvar "kps-delta"))
          (action "set-angle"
            (if (> 59.8 (array-element "actual" 4))
                (command "set-angle" (realvar "dps-delta")))))
        ;;(wait-with-tolerance (- (intvar "period") (mod (lookup-with-tolerance "time" .1) 1)) .1 "Wait")
        ;;ERROR: DarwinTimeAdapter.cc:216: LookupOnChange: Internal error: couldn't find smallest tolerance
        ;;Assertion failed: (false), function handleAssert, file Error.cc, line 89.
        (wait-with-tolerance (- (intvar "period") (mod (lookup-now "time") 1)) .1 "Wait")
        ))
    (action "Quit"
      (command "print" "\n[" (stringvar "name") "] sent: Quit (quitting)\n")
      (command "SendMessage" "Quit"))))

;; EOF
