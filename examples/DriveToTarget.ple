// A Plexil plan illustrating a simple rover driving task.

DriveToTarget:
{
  Boolean drive_done = false, timeout = false;
  Concurrence:

    Drive:
    {
      Command: rover_drive(10);
    }

    StopForTimeout:
    {
      StartCondition: LookupOnChange(time) >= 10;
      Concurrence:
        Stop:
        {
          Command: rover_stop();
        }
        SetTimeoutFlag:
        {
          Assignment: timeout = true;
        }
    }

    StopForTarget:
    {
      StartCondition: LookupOnChange(target_in_view);
      SkipCondition: timeout;
      Concurrence:
        Stop:
        {
          Command: rover_stop();
        }
        SetDriveFlag:
        {
          Assignment: drive_done = true;
        }
    }

    TakeNavcam:
    {
      StartCondition: timeout;
      SkipCondition: drive_done;
      Command: take_navcam();
    }

    TakePancam:
    {
      StartCondition: drive_done;
      SkipCondition: timeout;
      Command: take_pancam();
    }

    Heater:
    {
      SkipCondition: timeout;
      StartCondition: LookupOnChange(temperature) < 0;
      EndCondition: LookupOnChange(temperature) >= 10;
      RepeatCondition: true;
      Command: turn_on_heater();
    }
}
