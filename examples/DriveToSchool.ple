Integer Lookup time;
Boolean Lookup at_school;

DriveToSchool:
{
  Sequence
  
    StartCar:
    {
    	PostCondition: KeyStart.outcome == SUCCESS || PushStart.outcome == SUCCESS;
	EndCondition: KeyStart.outcome == SUCCESS || PushStart.outcome == SUCCESS;
	NodeList:
	
	  KeyStart:
	  {
	    PostCondition: LookupNow(car_started);
	    NodeList:
	    
	      PrimeAccelerator:
	      {
		For 
		( Integer count = 0; count < 3; count + 1 )
		  PressAccelerator: 
		  {
		    Command: depress_accelerator();
		  }
		}
	      
	      DepressClutch:
	      {
	        StartCondition: PrimeAccelerator.state == FINISHED;
		Command: depress_clutch();
	      }	      
	      TurnKey:  
	      {	        
	        StartCondition: DepressClutch.state == FINISHED;
		EndCondition: LookupOnChange(car_started) || LookupOnChange(time) > 5.0; // TurnKey.state.EXECUTING.START + 
		Command: turn_key();
	      }	      	    
	  }
	  PushStart:
	  {
	    StartCondition: KeyStart.state == FINISHED;
	    PostCondition: LookupNow(car_started);
	    Command: push_start();
	  }
    }//end of startCar
    SelectStation:
    {
      Boolean test;    
      StartCondition: StartCar.state == FINISHED;
      NodeList:
        CheckPassengers:
	{
	  Assignment: test = LookupNow(no_passengers);
	}
	{
	If ( test )
	Then
	  Radio:
	  {
	  Command: select_radio();
	  }
	Else
	  CD:
	  {
	  Command:  select_CD();
	  }
	}
    }//end of SelectStation
    DriveUntilAtSchool:
    {
      StartCondition: SelectStation.state == FINISHED;
      NodeList:
      
        {While (!LookupNow(at_school))
	  {
	    NodeList:
	    	            	
            HandleRain:
	    {
	      NodeList:
	   
	        {
	        If(LookupNow(raining))
	        Then
	          {
		    NodeList:
		  
	            Wipers:
	            {
	              Command: turn_on_wipers();
	            }
	            Lights:
	            {
	              Command: turn_on_lights();
	            }
		  }//end of Then
	        Else
	          {
	          }
	        }//end of If Parent
	    }//end of Handle Rain
	  DriveABit:
	  {
	    EndCondition: LookupOnChange(at_school) || LookupOnChange(time) > 5; //DriveABit.state.EXECUTING.START + 
	    Command: drive_a_bit();
	  }	    
	  }//End of While Loop
	}//end of While Loop Parent	
      }//End of DriveUntilAtSchool
}//end of driveToSchool
